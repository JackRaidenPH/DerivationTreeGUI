plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id 'org.beryx.jlink' version '2.24.1'
    id 'org.gradlex.extra-java-module-info' version "1.4"
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group 'dev.jackraidenph.dertreegui'
version '0.2.5'

repositories {
    mavenCentral()
    maven { url "https://maven.pkg.github.com/SWI-Prolog/packages-jpl"  }
    maven { url 'https://jitpack.io' }
}

sourceCompatibility = '17'
targetCompatibility = '17'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'dev.jackraidenph.dertreegui'
    mainClass = 'dev.jackraidenph.dertreegui.Main'
}

dependencies {
    implementation('org.controlsfx:controlsfx:11.1.2')
    implementation('com.github.SWI-Prolog:packages-jpl:V8.3.2')
}

extraJavaModuleInfo {
    module('packages-jpl-V8.3.2.jar', 'org.jpl7', '8.3.2') {
        exports('org.jpl7')
        requires('java.logging')
    }
    failOnMissingModuleInfo.set(false)
}

javafx {
    version = '17.0.1'
    modules = ['javafx.controls', 'javafx.fxml']
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'app'
    }
}

jlinkZip {
    group = 'distribution'
}